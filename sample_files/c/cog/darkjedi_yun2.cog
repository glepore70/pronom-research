# Jedi Knight Cog Script
#
# DARKJEDI_YUN2.COG
#
# DARK JEDI Script - Yun (second encounter)
#
# [YB]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved

symbols

message     startup
message     newplayer
message     damaged
message     killed
message     user0
message     user1
message     pulse
message     timer
message     skill

thing       darkjedi                         mask=0xfff

flex        damage                           local
int         damageType                       local

material    tip_mat=saberyellow0.mat         local
material    side_mat=saberyellow1.mat        local

template    tpl_wall=+ssparks_wall           local
template    tpl_blood=+ssparks_blood         local
template    tpl_saber=+ssparks_saber         local

int         victim                           local
int         forcepower                       local
int         forcepulse                       local
int         okay                             local

# FORCE BLINDING
template    cone_tpl=+force_blind            local
sound       blindingSound=ForceBlind01.WAV   local
int         effectHandle=-1                  local

# FORCE PULL
sound       pullSound=ForcePull01.WAV        local

template    tpl=BryarPistol                  local
template    tpl1=BryarPistol                 local
template    tpl2=BryarPistol                 local
template    tpl3=STrifle                     local
template    tpl4=SingleDetonator             local
template    tpl5=Crossbow                    local
template    tpl6=RepeaterGun                 local
template    tpl7=RailGun                     local
template    tpl8=SingleSeqCharge             local
template    tpl9=ConcRifle                   local

int         PulledWeapon                     local

end

# ========================================================================================

code

startup:
newplayer:
   jkSetPersuasionInfo(darkjedi, 5, 10);
   jkSetSaberInfo(darkjedi, side_mat, tip_mat, 0.003, 0.001, 0.100, tpl_wall, tpl_blood, tpl_saber);
   jkSetFlags(darkjedi, 0x15);
   SetTimerEx(3, 2, 0, 0);

   Return;

# ........................................................................................

killed:
   SetPulse(0);

   if(effectHandle!=-1)
   {
      freeColorEffect(effectHandle);
      effectHandle = -1;
      AddDynamicAdd(victim, 180, 260, 230);
   }
   ClearActorFlags(victim, 0x800);

   ClearActorFlags(darkjedi, 0x80);
   jkClearFlags(darkjedi, 0x20);
   SetThingCurGeoMode(darkjedi, GetThingGeoMode(darkjedi));

   jkSetFlags(darkjedi, 0x8);

   Return;

# ........................................................................................

damaged:
   damage = GetParam(0);
   damageType = GetParam(1);

   if(damageType & 1)                        // IMPACT
      damage = damage / 2;
   else
   if(damageType & 2)                        // ENERGY
      damage = damage / 2;
   else
   if(damageType & 4)                        // FIRE
      damage = damage / 10;
   else
   if(damageType & 8)                        // FORCE
      damage = damage;
   else
   if(damageType & 16)                       // SABER
      damage = damage;

   ReturnEx(damage);
   Return;

# ........................................................................................

pulse:
   if(forcepulse == 4)                       // FORCE PULL
   {
      SetPulse(0);
      if(GetHealth(victim) > 0)
      {
         if((GetCurWeapon(victim) != 1) && (GetCurWeapon(victim) != 10))
         {
            PlayMode(darkjedi, 35);
            Sleep(0.3);

            PlaySoundThing(pullSound, victim, 1.0, -1, -1, 0x80);

            // Create a powerup corresponding to the current weapon
            PulledWeapon = CreateThingAtPos(tpl[GetCurWeapon(victim)], GetThingSector(victim), GetThingPos(victim), '0 0 0');

            // Set the created powerup to no collide for now...
            SetCollideType(PulledWeapon, 0);

            // ...and set a timer to make it collide again later
            SetTimerEx(0.3, 1, PulledWeapon, 0);

            // Throw the powerup at the DJ
            ApplyForce(PulledWeapon, VectorScale(VectorNorm(VectorSub(GetThingPos(darkjedi), GetThingPos(victim))), 30));

            // Remove the weapon from the player
            ChangeInv(victim, GetCurWeapon(victim), -1);

            // Autoselect the best weapon left if that option is set
            if(GetAutoSwitch() & 1)
               SelectWeapon(victim, AutoSelectWeapon(victim, 1));
         }
      }
   }

   else

   if(forcepulse == 6)                       // FORCE PERSUASION
   {
      ClearActorFlags(darkjedi, 0x80);
      jkClearFlags(darkjedi, 0x20);
      SetThingCurGeoMode(darkjedi, GetThingGeoMode(darkjedi));
      SetPulse(0);
   }

   else

   if(forcepulse == 7)                       // FORCE BLINDING
   {
      if(effectHandle!=-1)
      {
         freeColorEffect(effectHandle);
         effectHandle = -1;
         AddDynamicAdd(victim, 180, 260, 230);
      }
      ClearActorFlags(victim, 0x800);
      SetPulse(0);
   }

   Return;

# ........................................................................................

user1:
   // Receives message from 17b_camera.cog once game starts
   okay = 1;
   return;


user0:
   // Don't start until receved message from 17b_camera.cog
   if(okay == 0) Return;

   victim = GetParam(0);
   forcepower = GetParam(1);

   if(forcepower == 1)
   {
      // Print("Force Power 1 : FORCE PERSUASION...");
      PlayMode(darkjedi, 24);
      Sleep(0.3);
      // He could die during the sleep
      if(GetThingHealth(darkjedi) <= 0) Return;

      SetActorFlags(darkjedi, 0x80);
      SetThingCurGeoMode(darkjedi, 0);
      jkSetFlags(darkjedi, 0x20);
      forcepulse = 6;
      SetPulse(10.0);
   }
   else
   if(forcepower == 2)
   {
      // Print("Force Power 2 : FORCE BLINDING...");
      if(effectHandle == -1)
      {
         PlayMode(darkjedi, 24);
         Sleep(0.3);
         // He could die during the sleep
         if(GetThingHealth(darkjedi) <= 0) Return;

         dummy = CreateThingAtPos(cone_tpl, GetThingSector(darkjedi), VectorAdd(GetThingPos(darkjedi), '0.0 0.0 0.04'), '0.0 0.0 0.0');
         SetThingLook(dummy, VectorSub(GetThingPos(victim), GetThingPos(darkjedi)));
         PlaySoundThing(blindingSound, darkjedi, 1.0, -1, -1, 0x80);

         // If the player doesn't have Seeing active.
         if(!IsInvActivated(victim, 23))
         {
            effectHandle = newColorEffect(0, 0, 0, 0, 0, 0, 180, 260, 230, 1.0);
            forcepulse = 7;
            SetActorFlags(victim, 0x800);
            SetPulse(5.0);
         }
      }
   }
   else
   if(forcepower == 3)
   {
      // Print("Force Power 3 : FORCE PULL...");
      if(!IsInvActivated(victim, 28))
      {
         forcepulse = 4;
         SetPulse(0.1);
      }
   }

   Return;

# ........................................................................................

timer:
   if(GetSenderId() == 1)                    // Pull
   {
      // A pulled weapon must be made takeable again
      SetCollideType(GetParam(0), 1);

      // Set gravity to them so they don't float around
      SetPhysicsFlags(GetParam(0), 1);

      // Make the powerup last hours
      SetLifeLeft(GetParam(0), 36000);
   }
   else
   if(GetSenderId() == 2)                    // blinding protection
   {
      // This makes the DJ nearly immune to blinding
      // by removing the blind flag every 3 seconds
      ClearActorFlags(darkjedi, 0x800);
      SetTimerEx(3, 2, 0, 0);
   }

   Return;

# ........................................................................................

skill:
   bin = GetParam(0);
   if(bin == 31)                             // Force Grip
   {
      ReturnEx(10);
      Return;
   }
   else
   if(bin == 34)                             // Deadly Sight
   {
      ReturnEx(5);
      Return;
   }

   ReturnEx(-1);
   Return;

end


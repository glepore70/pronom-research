# Jedi Knight Cog Script
#
# DARKJEDI_GORC.COG
#
# DARK JEDI Script - Gorc
#
# [YB]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved

symbols

message     startup
message     newplayer
message     damaged
message     killed
message     user0
message     user1
message     pulse
message     skill

thing       darkjedi                         mask=0xfff

material    tip_mat=saberorange0.mat         local
material    side_mat=saberorange1.mat        local

template    tpl_wall=+ssparks_wall           local
template    tpl_blood=+ssparks_blood         local
template    tpl_saber=+ssparks_saber         local

int         victim                           local
int         forcepower                       local
int         forcepulse                       local
int         okay=0                           local

template    repel=+force_repel               local

flex        starthealth                      local
int         griptrackID=-1                   local

end

# ========================================================================================

code

startup:
newplayer:
   jkSetSaberInfo(darkjedi, side_mat, tip_mat, 0.009, 0.003, 0.185, tpl_wall, tpl_blood, tpl_saber);
   jkSetFlags(darkjedi, 0x15);

   Return;

# ........................................................................................

killed:
   SetPulse(0);

   // Make sure the player is not frozen
   ClearActorFlags(GetLocalPlayerThing(), 0x40000);

   jkSetFlags(darkjedi, 0x8);

   Return;

# ........................................................................................

damaged:
   damage = GetParam(0);
   damageType = GetParam(1);

//Print("Damaged...");

   if(damageType & 1)                        // IMPACT
      damage = damage / 2;
   else
   if(damageType & 2)                        // ENERGY
      damage = damage / 2;
   else
   if(damageType & 4)                        // FIRE
      damage = damage / 10;
   else
   if(damageType & 8)                        // FORCE
      damage = damage;
   else
   if(damageType & 16)                       // SABER
      damage = damage;

   ReturnEx(damage);
   Return;

# ........................................................................................

pulse:
   if(forcepulse == 10)                      // FORCE GRIP
   {
      if((count > limit) ||
         (GetThingHealth(darkjedi) < starthealth) ||
         (GetHealth(victim) < 1) ||
         !HasLOS(darkjedi, victim)
        )
      {
         ClearActorFlags(victim, 0x40000);
         ClearActorFlags(darkjedi, 0x40000);
         SetPulse(0);
         Return;
      }

      // Print("You are taking Grip damage");
      jkPrintUNIString(victim, 300);

      DamageThing(victim, 5, 0x8, darkjedi);

      if(griptrackID != -1)
      {
         StopKey(player, griptrackID);
         griptrackID = -1;
      }
      count = count + 1;
   }

   Return;

# ........................................................................................

user1:
   // Receives message from 11_endlevel.cog once game starts
   okay = 1;
   return;


user0:
   // Don't start until receved message from 11_endlevel.cog
   if(okay == 0) Return;

   victim = GetParam(0);
   forcepower = GetParam(1);
   starthealth = GetThingHealth(darkjedi);

   if(forcepower == 1)
   {
      // Print("Force Power 1 : FORCE GRIP...");
      if(!IsInvActivated(victim, 29))
      {
         PlayMode(darkjedi, 24);
         SetActorFlags(darkjedi, 0x40000);
         Sleep(0.3);
         // He could die during the sleep
         if(GetThingHealth(darkjedi) <= 0) Return;

         SetActorFlags(victim, 0x40000);
         PlayMode(victim, 25);
         count = 0;
         limit = 5;
         forcepulse = 10;
         SetPulse(0.5);
      }
   }
   else
   if(forcepower == 2)
   {
      // Print("Force Power 2 : FORCE REPEL");
      PlayMode(darkjedi, 24);
      Sleep(0.3);
      // He could die during the sleep
      if(GetThingHealth(darkjedi) <= 0) Return;

      CreateThing(repel, darkjedi);
   }
   else
   if(forcepower == 3)
   {
      // Print("Force Power 3");
   }
   Return;

# ........................................................................................

skill:
   bin = GetParam(0);
   if(bin == 31)                             // Force Grip
   {
      ReturnEx(10);
      Return;
   }
   else
   if(bin == 34)                             // Deadly Sight
   {
      ReturnEx(5);
      Return;
   }

   ReturnEx(-1);
   Return;

end



